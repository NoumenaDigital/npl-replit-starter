package demo

/**
 * Struct to represent a timestamped payment entry
 * @param amount The amount of the payment
 * @param timestamp The time at which the payment was made
 */
struct Payment {
    amount: Number,
    timestamp: DateTime
};

/**
 * Simple IOU (I Owe You) protocol demonstrating NPL capabilities.
 *
 * This protocol models a debt relationship between an borrower and a lender,
 * with support for partial payments and debt cancellation.
 *
 * @param borrower The party who owes the money
 * @param lender The party who is owed the money
 * @param initialDebt The initial amount owed as specified by the IOU
 */
@api
protocol[borrower, lender] Iou(var initialDebt: Number) {
    require(initialDebt > 0, "Initial debt must be strictly positive");

    initial state due;
    state awaitingPaymentConfirmation;
    final state settled;
    final state cancelled;

    var paymentClaim: Optional<Payment> = optionalOf<Payment>();
    var payments: List<Payment> = listOf<Payment>();
    var remainingDebt: Number = initialDebt;

    /**
     * Claim a payment has been made towards the IOU.
     * Can only be called by the borrower while the IOU is due, i.e. before the debt is fully paid or cancelled, and
     * while no other payment is awaiting confirmation.
     * @param amount The amount to pay (must be positive and not exceed amount owed)
     */
    @api
    permission[borrower] pay(amount: Number) | due {
        require(amount > 0, "Amount must be strictly positive");
        require(amount <= remainingDebt, "Amount may not exceed remaining debt");

        paymentClaim = optionalOf(
            Payment(amount = amount, timestamp = now())
        );

        become awaitingPaymentConfirmation;

    };

    /**
     * Confirm a payment claimed by the borrower.
     * Can only be called by the lender while a payment is awaiting confirmation.
     */
    @api
    permission[lender] confirmPayment() | awaitingPaymentConfirmation {

        var confirmedPayment = paymentClaim.getOrFail();
        remainingDebt = remainingDebt - confirmedPayment.amount;
        payments = payments.with(confirmedPayment);
        paymentClaim = optionalOf<Payment>();

        if (remainingDebt > 0) {
            become due;
        } else {
            become settled;
        };
    };

    /**
     * Cancel the remaining debt.
     * Can only be called by the lender while the IOU is due.
     */
    @api
    permission[lender] cancel() | due, awaitingPaymentConfirmation {
        remainingDebt = 0;
        become cancelled;
    };
}
